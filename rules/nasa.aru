
-- This rule file corresponds to the NASA document
-- "Goddard Dynamic Simulator, Ada Coding Standard"
-- Stephen Leake, 14 April 2014
-- (https://stephe-leake.org/ada/code_standards_ada.pdf)

-- 2 Language
"2/2": check pragmas (nonstandard);
"2/3": check pragmas (not Unreferenced);
"2/4": check characters (not_iso_646, control, trailing_space),
       check max_line_length (120);

-- 3 File and package naming convention
"3.2/2": check naming_convention (generic, "^gen_", "^generic_");

-- 4 File layout
"4/1": check header_comments (model, "nasa.hdr");
"4/2": check style (multiple_elements, flexible clause);

-- 5 Style enforcing tools

-- 6 Comments
-- Trace special comments (error for "fixme", warnings for others)
"6/7": check comments (pattern, "fixme:");
"6/7": search comments (pattern, "workaround:");
"6/7": search comments (pattern, "improveme:");

-- 7 Identifier naming conventions
"7/1": check naming_convention (all, not "^?$");          -- More than one character...
"7/1": check naming_convention (root loop_control, ".*"); -- ... except for loops
"7/2": check style (casing_keyword, lowercase);
"7/3,4": check naming_convention (all, case_sensitive file "nasa_acronyms.txt"),
         check naming_convention (all, case_sensitive "^[A-Z][a-z0-9]*(_[A-Z0-9][a-z0-9]*)*$");
-- Either acronyms from file nasa_acronyms.txt or mixed case with underscores
"7/5": check naming_convention (type, "_Type$");
"7/6": check style (casing_identifier, original);
"7/7": check style (no_closing_name);

-- 8 Indentation
"8/4": check style (multiple_elements, statement);

-- 9 Subprograms
"9.1/2": check style (default_in);
"9.1/3": check derivations (indicator, overriding, declaration);
"9.3/3": search positional_associations (same_type, 1, call);

-- 11 Statements
"11.1": check declarations (multiple_names); 
"11.4/2": check case_statement (others_span, min 1);

-- 12 Miscellaneous
-- REM 12.1 redundant with 9.3/3, + aggregates
"12.1": search positional_associations (same_type, 1, array_aggregate),
        search positional_associations (same_type, 1, record_aggregate);  -- Subprograms are handled in 9.3/3
"12.2": check positional_associations (all, 0, instantiation);
"12.3": check reduceable_scope (use),
        check use_clauses (global),
        check use_clauses (type_global),
        check use_clauses (all_type_global);
"12.4": check exception_propagation (task);
"12.5": search entities (Ada.Exceptions.Raise_Exception);
--"12.7" redundant with 9.1/3
"12.8": search declarations (null_procedure_body);

